plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.flywaydb.flyway' version '6.2.3'
    id 'java'
}

group = 'dev.abgeo'
version = '0.0.1'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time'

    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
    implementation 'javax.servlet:jstl'
    implementation 'org.flywaydb:flyway-core'

    runtimeOnly 'mysql:mysql-connector-java'

    // WebJars
    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:jquery:3.2.1'
    implementation 'org.webjars:popper.js:2.9.2'
    implementation 'org.webjars:sockjs-client:1.1.2'
    implementation 'org.webjars:stomp-websocket:2.3.3'
    implementation 'org.webjars:bootstrap:4.3.1'
    implementation 'org.webjars:font-awesome:4.7.0'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

def props = new Properties()
file('src/main/resources/application.properties').withInputStream { props.load(it) }

flyway {
    url = props.getProperty('spring.datasource.url')
    user = props.getProperty('spring.datasource.username')
    password = props.getProperty('spring.datasource.password')
    locations = [
            'filesystem:src/main/resources/db/migration'
    ]
}
